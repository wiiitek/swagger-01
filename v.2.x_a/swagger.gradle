def generatedModule = file("$projectDir/../v.2.x_a_generated")
def generatedModulePath = generatedModule.absolutePath
def swaggerInputDir = '../api-specification/'
def packagePrefix = 'pl.kubiczak.test.swagger.v2.x.a.generated.'

swaggerSources {
  TasksV1 {
    inputFile = file(swaggerInputDir + 'tasks-v1.yaml')
    code {
      outputDir = generatedModule
      language = 'spring'
      additionalProperties = [
        'modelPackage': packagePrefix + 'tasks.model',
        'apiPackage'  : packagePrefix + 'tasks.api',
        'dateLibrary' : 'java8'
      ]
      components = ['models', 'apis']
    }
  }
}

sourceSets.main.java.srcDir "${swaggerSources.TasksV1.code.outputDir}/src/main/java"

compileJava.dependsOn swaggerSources.TasksV1.code

// we want to delete generate controllers as we will implement them by ourselves
task TasksV1ControllersDelete {
  doLast {
    println generatedModulePath
    delete fileTree(generatedModulePath) {
      include '**/tasks/api/*Controller.java'
    }
  }
}
generateSwaggerCodeTasksV1.finalizedBy TasksV1ControllersDelete


// clean should also delete generated code
task cleanGeneratedCode {
  doLast {
    delete files(generatedModulePath)
  }
}
clean.dependsOn cleanGeneratedCode
